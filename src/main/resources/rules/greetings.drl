package org.integrallis.greeting

import org.integrallis.greeting.Person.Education;
import org.integrallis.greeting.Person.Gender;
import org.integrallis.greeting.Person.MaritalStatus;

/**
 * GreetingAndSalutation Rules
 */
 
rule "GreetingAndSalutationGoodMorning"
	when
		TimeOfDay(hour < 12)
		person:Person()
	then 
		GreetingAndSalutation greeting = new GreetingAndSalutation();
		greeting.setGreeting("Good Morning");
		greeting.setPerson(person);
		insert(greeting);
end

rule "GreetingAndSalutationGoodAfternoon"
	when
		TimeOfDay(hour >= 12, hour <= 17)
		person:Person()
	then 	
		GreetingAndSalutation greeting = new GreetingAndSalutation();
		greeting.setGreeting("Good Afternoon");
		greeting.setPerson(person);
		insert(greeting);
end

rule "GreetingAndSalutationGoodEvening"
	when
		TimeOfDay(hour >= 18, hour <= 22)
		person:Person()
	then 	
		GreetingAndSalutation greeting = new GreetingAndSalutation();
		greeting.setGreeting("Good Evening");
		greeting.setPerson(person);
		insert(greeting);
end

rule "GreetingAndSalutationGoodNight"
	when
		TimeOfDay(hour >= 23, hour <= 24)
		person:Person()
	then 	
		GreetingAndSalutation greeting = new GreetingAndSalutation();
		greeting.setGreeting("Good Night");
		greeting.setPerson(person);
		insert(greeting);
end

/**
 * Salutation Rules
 */

rule "SalutationMrs"
	when
		person:Person(personName: name, gender == Gender.FEMALE, maritalStatus == MaritalStatus.MARRIED, education != Education.PHD)
		greeting:GreetingAndSalutation(person == person, salutation == null)
	then 
		greeting.setSalutation("Mrs.");
		update(greeting);
end

rule "SalutationMs"
	when
		person:Person(personName: name, gender == Gender.FEMALE, maritalStatus == MaritalStatus.SINGLE, education != Education.PHD)
		greeting:GreetingAndSalutation(person == person, salutation == null)
	then 	
		greeting.setSalutation("Ms.");
		update(greeting);
end

rule "SalutationMr"
	when
		person:Person(personName: name, gender == Gender.MALE, education < Education.PHD, age > 10)
		greeting:GreetingAndSalutation(person == person, salutation == null)
	then 
		greeting.setSalutation("Mr.");
		update(greeting);	
end

rule "SalutationDr"
	when
		person:Person(personName: name, education >= Education.PHD)
		greeting:GreetingAndSalutation(person == person, salutation == null)
	then 	
		greeting.setSalutation("Dr.");
		update(greeting);
end

rule "SalutationLittle"
	when
		person:Person(personName: name, gender == Gender.MALE, maritalStatus == MaritalStatus.SINGLE, age <= 10)
		greeting:GreetingAndSalutation(person == person, salutation == null)
	then 
		greeting.setSalutation("Little");
		update(greeting);
end

rule "PrintAll"
    enabled false
	when
	    greeting: GreetingAndSalutation(salutation != null, greeting != null)
	then
	    System.out.println(greeting.greetAndSalute());
end

query "GetAllGreetingsAndSalutations"
  greeting : GreetingAndSalutation(salutation != null, greeting != null)
end